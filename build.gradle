buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'idea'
}
apply plugin: 'net.minecraftforge.gradle'

/**
 * Version in dev is set to 'dev'
 * Any builds produced by the github actions build script will have a version matching their tag.
 * All version numbers should be Semantic Versioning 2.0 compliant
 */
if (System.getenv('VERSION') != null) {
    version = System.getenv('VERSION')
} else {
    version = 'dev'
}
group = 'com.alcatrazescapee.oreveins' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "oreveins-forge-${mc_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

// Exclude the test veins when not in dev
if (version != 'dev') {
    sourceSets {
        main {
            resources {
                exclude 'data/oreveins/oreveins/tests**'
            }
        }
    }
}

repositories {
    maven { url 'https://jitpack.io' }
}

minecraft {
    mappings channel: 'snapshot', version: '20200408-1.15.1'

    // Run configurations
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                oreveins {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                oreveins {
                    source sourceSets.main
                }
            }
        }

        serverTest {
            parent server // This run config inherits settings from the server config
            workingDirectory project.file('run')
            main 'com.alcatrazescapee.mcjunitlib.DedicatedTestServerLauncher' // The main class which launches a customized server which then runs JUnit tests
            ideaModule "${project.name}.test" // Tell IDEA to use the classpath of the test module
            property 'forge.logging.console.level', 'unittest' // This logging level prevents any other server information messages and leaves only the unit test output
            environment 'MOD_CLASSES', String.join(File.pathSeparator,
                    "oreveins%%${sourceSets.main.output.resourcesDir}",
                    "oreveins%%${sourceSets.main.output.classesDir}",
                    "oreveins%%${sourceSets.test.output.resourcesDir}",
                    "oreveins%%${sourceSets.test.output.classesDir}",
            ) // Forge will ignore all test sources unless we explicitly tell it to include them as mod sources
            environment 'target', 'fmltestserver' // This is a custom service used to launch with ModLauncher's transforming class loader
            mods {
                oreveins { // The mod that is being tested
                    sources sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    testImplementation fg.deobf('com.github.alcatrazEscapee:mcjunitlib:1.0.0-1.15.2') {
        transitive = false
    }
}

// Puts the mod version into the jar for mods.toml to read from
jar {
    manifest {
        attributes "Implementation-Version": "${version}"
    }
}

curseforge {
    def envApiKey = System.getenv('CURSEFORGE_API_KEY')
    if (envApiKey == null) {
        println 'Unable to locate api key in env variable'
        return
    }
    def versionReleaseType = "release"
    if (version.toString().contains("beta")) {
        versionReleaseType = "beta"
    } else if (version.toString().contains("alpha")) {
        versionReleaseType = "alpha"
    }
    project {
        apiKey = envApiKey
        id = '296887'
        changelog = "[Release Notes ${version}](https://github.com/alcatrazEscapee/ore-veins/releases/tag/v${version})"
        changelogType = "markdown"
        releaseType = versionReleaseType
        addGameVersion "${mc_version}"
    }
}